theory P5d
begin

builtins: symmetric-encryption, hashing, diffie-hellman
functions: mac/2, map/2

// Shared symmetric key setup and reveal

rule Shared_Key:
  [ Fr(~skAB) ]
  --[ NotEq($A, $B) ]->
  [ !SKey($A, $B, ~skAB) ] 

rule Reveal_SK:
  [ !SKey(A, B, skAB) ] 
  --[ Reveal(A), Reveal(B) ]-> 
  [ Out(skAB) ]


/* We formalize the following protocol
  protocol {
    1. A -> B: { g^x, {s}h(K(A,B)), p }
    2. A <- B: { g^y }
    -- Key Generation using Kab = h(g^(xy)) --
    3. A -> B: { g^y }Kab
    4. A <- B: { g^x }Kab
  }

*/	

// Initialization

rule Init_A:
  [ Fr(~id) 
  , !SKey(I, R, skAB)
  ]
  --[ Create_A(I, ~id) ]->
  [ St_A_1(I, ~id, skAB, R) ] 

rule Init_B:
  [ Fr(~id) 
  , !SKey(I, R, skAB)
  ]
  --[ Create_B(R, ~id) ]->
  [ St_B_1(R, ~id, skAB, I) ] 

// Formalization of 1. for both A and B

rule A_1_Send:
  let
     g = map(~s, 'p')
     encS = senc{~s}h(skAB)
     mA1 = <g^~x, <encS, 'p'>>
  in
  [ St_A_1(I, ~id, skAB, R)
  , Fr(~x)
  , Fr(~s)
  ]
  --[ Send(I, mA1) ]->
  [ St_A_2(I, ~id, skAB, R, ~x, ~s, 'p') 
  , Out(mA1)
  ]

rule B_1_Receive:
  let 
     encS = senc{s}h(skAB)
     mB1 = <gX, <encS, 'p'>>
  in
  [ St_B_1(R, ~id, skAB, I)
  , In(mB1)
  ]
  --[ Receive(R, mB1) ]->
  [ St_B_2(R, ~id, skAB, I, gX, s, 'p') ]


// Formalization of 2. for both A and B

rule B_2_Send:
  let
     g = map(s, 'p')
  in
  [ St_B_2(R, ~id, skAB, I, gX, s, 'p')
  , Fr(~y)
  ]
  --[ Send(R, g^~y) ]->
  [ St_B_3(R, ~id, skAB, I, gX, s, 'p', ~y)
  , Out(g^~y)
  ]

rule A_2_Receive:
  let
     g = map(s, 'p')
  in
  [ St_A_2(I, ~id, skAB, R, x, s, 'p') 
  , In(gY)
  ]
--[ Receive(I, gY)
  ,  NotEq(g^x, gY)
  ]->
  [ St_A_3(I, ~id, skAB, R, x, s, 'p', gY) ]

// Formalization of 3. for both A and B

rule A_3_Send:
  let 
     g = map(s, 'p')
     Kab = h(gY^x)
     mA3 = mac(gY, Kab)
  in
  [ St_A_3(I, ~id, skAB, R, x, s, 'p', gY) ]
--[ Send(I, mA3) 
  , Running(I, R, <'R', 'I', <g^x, gY, Kab>>)
  ]->
  [ St_A_4(I, ~id, skAB, R, x, s, 'p', gY)
  , Out(mA3)
  ]

rule B_3_Receive:
  let 
     g = map(s, 'p')
     Kab = h(gX^y)
     mB3 = mac(g^y, Kab)
  in
  [ St_B_3(R, ~id, skAB, I, gX, s, 'p', y) 
  , In(mB3)
  ]
--[ Receive(R, mB3) 
  , Running(R, I, <'I', 'R', <gX, g^y, Kab>>)
  ]->
  [ St_B_4(R, ~id, skAB, I, gX, s, 'p', y) ]


// Formalization of 4. for both A and B

rule B_4_Send:
  let
     g = map(s, 'p')
     Kab = h(gX^y)
     mB4 = mac(gX, Kab)
  in
  [ St_B_4(R, ~id, skAB, I, gX, s, 'p', y) ]
--[ Send(R, mB4) 
  , Secret(R, Kab)
  , Commit(R, I, <'R', 'I', <gX, g^y, Kab>>)
  , Honest(R)
  , Honest(I)
  ]->
  [ St_B_5(R, ~id, skAB, I, gX, s, 'p', y)
  , Out(mB4)
  ]

rule A_4_Receive:
  let
     g = map(s, 'p')
     Kab = h(gY^x)
     mA4 = mac(g^x, Kab)
  in
  [ St_A_4(I, ~id, skAB, R, x, s, 'p', gY)
  , In(mA4)
  ]
--[ Receive(I,mA4)
  , Secret(I, Kab)
  , Commit(I, R, <'I', 'R', <g^x, gY, Kab>>)
  , Honest(I)
  , Honest(R)
  , Finish() 
  ]->
  [ St_A_5(I, ~id, skAB, R, x, s, 'p', gY) ]


// Lemmas

// All of them get verified

lemma executable:
  exists-trace
    "
      Ex #i.
         Finish() @i 
       & not (Ex A #j. Reveal(A) @j) 
    "

lemma secrecy:
  "All A x #i. 
    Secret(A,x) @i ==> 
    not (Ex #j. K(x)@j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

lemma secrecy_PFS:
  "All A x #i. 
    Secret(A,x) @i ==> 
    not (Ex #j. K(x)@j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i & r < i)"

lemma noninjectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

lemma injectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j 
        & not (Ex a2 b2 #i2. Commit(a2,b2,<'I','R',t>) @i2
                           & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"


lemma noninjectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

lemma injectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j 
              & not (Ex a2 b2 #i2. Commit(a2,b2,<'R','I',t>) @i2
                                 & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"


// Restrictions 

restriction Inequality:
  " All X Y #i. NotEq(X, Y) @i ==> not (X = Y) "

end